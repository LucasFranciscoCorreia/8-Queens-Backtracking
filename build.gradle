plugins {
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version '2.1.0'
  id 'org.javamodularity.moduleplugin' version '1.8.15'
  id 'org.openjfx.javafxplugin' version '0.1.0'
  id "org.beryx.jlink" version "3.1.1"
}

group = 'br.ufrpe'
version = '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

application {
  mainModule = 'br.ufrpe.n8queens'
  mainClass = 'br.ufrpe.n8queens.N8QueensUI'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/kotlin']
        }
    }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.0"
  implementation "org.openjfx:javafx-controls:23"
  implementation "org.openjfx:javafx-fxml:23"
  implementation 'io.github.palexdev:materialfx:11.13.6'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'n8queens'
  }
  jpackage {
    installerOptions = ['--win-menu', '--win-shortcut']
    installerType = 'exe'
  }
}

kotlin {
  jvmToolchain( 23 )
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(23))
  }
}

javafx {
  version = '23'
  modules = ['javafx.controls', 'javafx.fxml']
}

tasks.jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
